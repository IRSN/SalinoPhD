*DECK T16MPS
      SUBROUTINE T16MPS(IPCPO ,IPRINT,MAXMIX,MNLOCP,MNCPLP,MNPERT,
     >                  NALOCP,IDLCPL,NCMIXS,NGCCPO,ITITL ,LSUBT ,
     >                  ISUBT ,
     >                  ENECPO,NAMMIX,MIXRCI,PARRCI,PARPER)
C
C----
C  1- PROGRAMME STATISTICS:
C      NAME     : T16MPS
C      USE      : SAVE MIXTURE PROCESSING OPTIONS ON CPO
C      AUTHOR   : G.MARLEAU
C      CREATED  : 1999/10/21
C      REF      : IGE-244 REV.1
C
C      MODIFICATION LOG
C      --------------------------------------------------------------
C      | DATE AND INITIALS  | MOTIVATIONS
C      --------------------------------------------------------------
C      | 1999/10/21 G.M.    | READ FROM INPUT T16CPO
C      |                    | PROCESSING OPTIONS
C      --------------------------------------------------------------
C
C  2- ROUTINE PARAMETERS:
C    INPUT
C      IPCPO  : POINTER TO THE CPO STRUCTURE             I
C      IPRINT : PRINT LEVEL                              I
C               >=  10 PRINT ACTIVATED
C      MAXMIX : MAXIMUM NUMBER OF MIXTURE PERMITTED      I
C      MNLOCP : MAXIMUM NUMBER OF LOCAL PARAMETERS       I
C      MNCPLP : MAXIMUM NUMBER OF COUPLED  PARAMETERS    I
C      MNPERT : MAXIMUM NUMBER OF PERTURBATION PER       I
C               LOCAL PARAMETERS
C      NALOCP : LOCAL PARAMETER NAMES PERMITTED          C(MNLOCP
C                                                        +MNCPLP)*4
C      IDLCPL : LOCAL PARAMETER ID FOR PERTURBATION      I(2,MNLOCP
C               PARAMETER                                 +MNCPLP)
C      NCMIXS : NUMBER OF CURRENT MIXTURES SAVED         I
C               TOTAL NUMBER OF MIXTURE SAVED
C      NGCCPO : NUMBER OF CPO GROUPS                     I
C      ITITL  : INTEGER VECTOR CONTAINING TITLE          I(18)
C      LSUBT  : LENGTH OF SUBTITLE                       I
C      ISUBT  : INTEGER VECTOR CONTAINING SUB-TITLE      I(LSUBT)
C      ENECPO : CPO ENERGY GROUP STRUCTURE               R(NGCCPO+1)
C      NAMMIX : NAME OF MIXTURE                          I(2,MAXMIX)
C               ALL MIXTURE NAMES TO SAVE
C               CONTAINS VARIABLE CHARACTER*6 NAME
C               READ(NAME,'(A4,A2)') (NAMMIX(J,*),J=1,2)
C      MIXRCI : REFERENCE INFORMATION FOR A MIXTURE      I(2+MNLOCP+
C               (O) = 0 NO INFORMATION FOR MIXTURE  MNCPLP,MAXMIX)
C                   > 0 INFORMATION NOT UPDATED
C                   < 0 INFORMATION TO BE UPDATED
C      PARRCI : REFERENCE LOCAL PARAMETERS FOR A         R(MNLOCP,
C               MIXTURE                                    MAXMIX)
C      PARPER : PERTURBATION PARAMETERS FOR A            R(MNPERT,2,
C               MIXTURE                      MNLOCP+MNCPLP,MAXMIX)
C
C  3- ROUTINES CALLED
C    UTILITIES ROUTINES
C      XABORT : ABORT ROUTINE
C      XDRSET : VECTOR INITIALIZATION ROUTINE
C      LCMGET : DATA STRUCTURE READ ROUTINE
C      LCMPUT : DATA STRUCTURE WRITE ROUTINE
C      LCMSIX : DATA STRUCTURE CHANGE DIRECTORY ROUTINE
C
C----
C
      USE GANLIB
      IMPLICIT         NONE
      TYPE(C_PTR)      IPCPO
      INTEGER          IPRINT,MAXMIX,MNLOCP,MNCPLP,MNPERT,
     >                 NCMIXS,NGCCPO,LSUBT
      CHARACTER        NALOCP(MNLOCP+MNCPLP)*4
      INTEGER          IDLCPL(2,MNLOCP+MNCPLP),ITITL(18),
     >                 ISUBT(LSUBT),NAMMIX(2,MAXMIX),
     >                 MIXRCI(2+MNLOCP+MNCPLP,MAXMIX)
      REAL             ENECPO(NGCCPO+1),PARRCI(MNLOCP,MAXMIX),
     >                 PARPER(MNPERT,2,MNLOCP+MNCPLP,MAXMIX)
C----
C  MEMORY ALLOCATION
C----
      INTEGER, ALLOCATABLE, DIMENSION(:) :: IDSUF,NBPER
      REAL, ALLOCATABLE, DIMENSION(:) :: LOCALP
C----
C  LOCAL VARIABLES
C----
      INTEGER          IOUT,NPARAM,NTC,ILCMUP,ILCMDN
      CHARACTER        NAMSBR*6,NAMMAC*12
      PARAMETER       (IOUT=6,NPARAM=4,ILCMUP=1,ILCMDN=2,
     >                 NTC=3,NAMSBR='T16MPS',NAMMAC='MACR        ')
      CHARACTER        NAMDIR*12
      INTEGER          IPARAM(NPARAM),KCHAR(NTC),
     >                 NBURN,ILOCP,IMIX,IGC,
     >                 IPER,NBPERP,ILCMLN,ITYLCM,NLPAR,ILPAR,ILOCL,
     >                 IR,NPERTN,NMODRC
C----
C  SAVE MIXTURE NAMES
C----
      NAMDIR=NAMMAC
      READ(NAMDIR,'(3A4)') (KCHAR(IR),IR=1,NTC)
      NPERTN=MNLOCP+MNCPLP
      CALL LCMPUT(IPCPO,'MIXTURE-PREF',2*NCMIXS,3,NAMMIX)
C----
C  SAVE PERTURBATION SUFFIX NAMES
C----
      ALLOCATE(IDSUF(NPERTN))
      DO 100 ILOCP=1,NPERTN
        READ(NALOCP(ILOCP),'(A4)') IDSUF(ILOCP)
 100  CONTINUE
      CALL LCMPUT(IPCPO,'PERTURB-SUFX',NPERTN,3,IDSUF)
      DEALLOCATE(IDSUF)
C----
C  SAVE NUMBER OF PERTURBATION PER LOCAL PARAMETER PER MIXTURE
C----
      ALLOCATE(NBPER(NPERTN*NCMIXS))
      IPER=0
      DO 110 IMIX=1,NCMIXS
        DO 111 ILOCP=1,NPERTN
          IPER=IPER+1
          NBPER(IPER)=ABS(MIXRCI(2+ILOCP,IMIX))
 111    CONTINUE
 110  CONTINUE
      CALL LCMPUT(IPCPO,'PERTURB-NUMB',NPERTN*NCMIXS,
     >            1,NBPER)
      DEALLOCATE(NBPER)
      ALLOCATE(LOCALP(MNLOCP))
C----
C  SCAN OVER MIXTURES
C----
      DO 120 IMIX=1,NCMIXS
C----
C  MIXTURE TO UPDATE
C----
        WRITE(NAMDIR,'(A4,A2,A6)')
     >  NAMMIX(1,IMIX),NAMMIX(2,IMIX),'RC    '
        NBURN=ABS(MIXRCI(2,IMIX))
        IPARAM(1)=NGCCPO
        IPARAM(2)=1
        IPARAM(3)=2
        IPARAM(4)=NBURN
        NMODRC=0
        DO 121  ILOCP=1,NPERTN
          IF(MIXRCI(2+ILOCP,IMIX) .LT. 0) THEN
            NMODRC=NMODRC-1
          ENDIF
 121    CONTINUE
        IF(MIXRCI(2,IMIX) .LT. 0 ) THEN
          CALL LCMSIX(IPCPO,NAMDIR,ILCMUP)
          CALL LCMLEN(IPCPO,'PARAM       ',ILCMLN,ITYLCM)
          IF(ILCMLN .EQ. 0) THEN
            CALL LCMPUT(IPCPO,'PARAM       ',NPARAM,1,IPARAM)
            CALL LCMPUT(IPCPO,'ENERGY      ',NGCCPO+1,2,ENECPO)
          ELSE
            CALL LCMGET(IPCPO,'PARAM       ',IPARAM)
            IF(IPARAM(1) .NE. NGCCPO .OR.
     >         IPARAM(2) .NE. 1      .OR.
     >         IPARAM(3) .NE. 2      .OR.
     >         IPARAM(4) .NE. NBURN      ) THEN
C----
C  ABORT SINCE REFERENCE CASE PARAMETERS HAVE CHANGED
C----
              CALL XABORT(NAMSBR//
     >        ': INCOMPATIBLE PARAMETERS FOR '//NAMDIR)
            ENDIF
          ENDIF
          CALL XDRSET(LOCALP,MNLOCP,0.0)
          DO 122 ILOCP=1,MNLOCP
            LOCALP(ILOCP)=PARRCI(ILOCP,IMIX)
 122      CONTINUE
          CALL LCMPUT(IPCPO,'LOCAL-PARAMS',MNLOCP, 2,LOCALP)
          CALL LCMPUT(IPCPO,'TITLE       ',    18, 3,        ITITL)
          CALL LCMPUT(IPCPO,'SUB-TITLE   ', LSUBT, 3,        ISUBT)
          CALL LCMPUT(IPCPO,'ISOTOPESNAME',   NTC, 3,        KCHAR)
          CALL LCMSIX(IPCPO,NAMDIR,ILCMDN)
        ELSE IF(NMODRC .LT. 0) THEN
          CALL LCMSIX(IPCPO,NAMDIR,ILCMUP)
          CALL XDRSET(LOCALP,MNLOCP,0.0)
          DO 123 ILOCP=1,MNLOCP
            LOCALP(ILOCP)=PARRCI(ILOCP,IMIX)
 123      CONTINUE
          CALL LCMPUT(IPCPO,'LOCAL-PARAMS',MNLOCP, 2,LOCALP)
          CALL LCMSIX(IPCPO,NAMDIR,ILCMDN)
        ENDIF
C----
C  PERTURBATIONS TO UPDATE
C----
        DO 130 ILOCP=1,NPERTN
          NBPERP=ABS(MIXRCI(2+ILOCP,IMIX))
          IF(MIXRCI(2+ILOCP,IMIX) .LT. 0) THEN
            NLPAR=1
            IF(ILOCP .GT. MNLOCP) NLPAR=2
            DO 131 IPER=1,NBPERP
              WRITE(NAMDIR,'(A4,A2,A4,I2)')
     >        NAMMIX(1,IMIX),NAMMIX(2,IMIX),NALOCP(ILOCP),IPER
              CALL LCMSIX(IPCPO,NAMDIR,ILCMUP)
              CALL LCMLEN(IPCPO,'PARAM       ',ILCMLN,ITYLCM)
              IF(ILCMLN .EQ. 0) THEN
                CALL LCMPUT(IPCPO,'PARAM       ',NPARAM,1,IPARAM)
                CALL LCMPUT(IPCPO,'ENERGY      ',NGCCPO+1,2,ENECPO)
              ELSE
                CALL LCMGET(IPCPO,'PARAM       ',IPARAM)
                IF(IPARAM(1) .NE. NGCCPO .OR.
     >             IPARAM(2) .NE. 1      .OR.
     >             IPARAM(3) .NE. 2      .OR.
     >             IPARAM(4) .NE. NBURN      ) THEN
C----
C  ABORT SINCE PERTURBATION PARAMETERS HAVE CHANGED
C----
                  CALL XABORT(NAMSBR//
     >            ': INCOMPATIBLE PARAMETERS FOR '//NAMDIR)
                ENDIF
              ENDIF
C----
C  TRANSFER REFERENCE PARAMETERS
C----
              DO 132 ILOCL=1,MNLOCP
                LOCALP(ILOCL)=PARRCI(ILOCL,IMIX)
 132          CONTINUE
C----
C  TRANSFER PERTURBED PARAMETERS
C----
              DO 133 ILPAR=1,NLPAR
                ILOCL=IDLCPL(ILPAR,ILOCP)
                LOCALP(ILOCL)=PARPER(IPER,ILPAR,ILOCP,IMIX)
 133          CONTINUE
              CALL LCMPUT(IPCPO,'LOCAL-PARAMS',MNLOCP, 2,LOCALP)
              CALL LCMPUT(IPCPO,'TITLE       ',    18, 3,        ITITL)
              CALL LCMPUT(IPCPO,'SUB-TITLE   ', LSUBT, 3,        ISUBT)
              CALL LCMPUT(IPCPO,'ISOTOPESNAME',   NTC, 3,        KCHAR)
              CALL LCMSIX(IPCPO,NAMDIR,ILCMDN)
 131        CONTINUE
          ENDIF
 130    CONTINUE
 120  CONTINUE
      DEALLOCATE(LOCALP)
C----
C  PROCESS PRINT LEVEL
C----
      IF(IPRINT .GE. 1) THEN
        WRITE(IOUT,6000) NAMSBR,NCMIXS,NGCCPO
        WRITE(IOUT,6030) (ENECPO(IGC),IGC=1,NGCCPO+1)
        DO 600 IMIX=1,NCMIXS
          IF(MIXRCI(2,IMIX) .LT. 0) THEN
            WRITE(IOUT,6010) (NAMMIX(IR,IMIX),IR=1,2),
     >                        ABS(MIXRCI(2,IMIX))
            WRITE(IOUT,6020)
     >        (NALOCP(ILOCP),PARRCI(ILOCP,IMIX),ILOCP=1,MNLOCP)
          ELSE IF(MIXRCI(2,IMIX) .GT. 0) THEN
            WRITE(IOUT,6011) (NAMMIX(IR,IMIX),IR=1,2),
     >                        ABS(MIXRCI(2,IMIX))
            WRITE(IOUT,6020)
     >      (NALOCP(ILOCP),PARRCI(ILOCP,IMIX),ILOCP=1,MNLOCP)
          ENDIF
          DO 610 ILOCP=1,NPERTN
            NBPERP=ABS(MIXRCI(2+ILOCP,IMIX))
            NLPAR=1
            IF(ILOCP .GT. MNLOCP) NLPAR=2
            IF(MIXRCI(2+ILOCP,IMIX) .LT. 0) THEN
              WRITE(IOUT,6012) NBPERP,NALOCP(ILOCP)
              DO 611 IPER=1,NBPERP
                WRITE(IOUT,6021) IPER,
     >          (NALOCP(IDLCPL(ILPAR,ILOCP)),
     >           PARPER(IPER,ILPAR,ILOCP,IMIX),
     >           ILPAR=1,NLPAR)
 611          CONTINUE
            ELSE IF(MIXRCI(2+ILOCP,IMIX) .GT. 0) THEN
              WRITE(IOUT,6013) NBPERP,NALOCP(ILOCP)
              DO 612 IPER=1,NBPERP
                WRITE(IOUT,6021) IPER,
     >          (NALOCP(IDLCPL(ILPAR,ILOCP)),
     >           PARPER(IPER,ILPAR,ILOCP,IMIX),
     >           ILPAR=1,NLPAR)
 612          CONTINUE
            ENDIF
 610      CONTINUE
 600    CONTINUE
        WRITE(IOUT,6001)
      ENDIF
C----
C  PRINT FORMAT
C----
 6000 FORMAT(1X,5('*'),'OUTPUT FROM ',A6,5('*')/
     >       6X,'CONTENTS OF CPO AFTER UPDATE'/
     >       6X,'NUMBER OF MIXTURES     = ',I10/
     >       6X,'NUMBER OF GROUPS       = ',I10)
 6001 FORMAT(1X,28('*'))
 6010 FORMAT(6X,'NAME OF MIXTURES    = ',A4,A4,
     >       2X,'NUMBER OF BURNUP    = ',I10,
     >       2X,'UPDATED FROM CURRENT TAPE16')
 6011 FORMAT(6X,'NAME OF MIXTURES    = ',A4,A4,
     >       2X,'NUMBER OF BURNUP    = ',I10,
     >       2X,'TAKEN FROM OLD CPO')
 6012 FORMAT(6X,I10,' PERTURBATIONS FOR ',A4,
     >       2X,'UPDATED FROM CURRENT TAPE16')
 6013 FORMAT(6X,I10,' PERTURBATIONS FOR ',A4,
     >       2X,'UPDATED FROM OLD CPO')
 6020 FORMAT(6X,'LOCAL PARAMETER FOR REFERENCE CASE'/
     >       1P,6(2X,A4,1X,E10.3))
 6021 FORMAT(6X,'LOCAL PARAMETER PERTURBATION = ',I2,
     >       1P,2(2x,A4,1X,E10.3))
 6030 FORMAT(6X,'CPO ENERGY STRUCTURE (EV)'/
     >1P,10(2X,E10.3))
      RETURN
      END
