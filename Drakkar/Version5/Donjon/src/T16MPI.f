*DECK T16MPI
      SUBROUTINE T16MPI(IPCPO ,IPRINT,MAXMIX,MNLOCP,MNCPLP,MNPERT,
     >                  NALOCP,IDLCPL,NCMIXS,NGCCPO,ENECPO,NAMMIX,
     >                  MIXRCI,PARRCI,PARPER)
C
C----
C  1- PROGRAMME STATISTICS:
C      NAME     : T16MPI
C      USE      : INITIALIZE MIXTURE PROCESSING OPTIONS
C                 FROM OLD CPO
C      AUTHOR   : G.MARLEAU
C      CREATED  : 1999/10/21
C      REF      : IGE-244 REV.1
C
C      MODIFICATION LOG
C      --------------------------------------------------------------
C      | DATE AND INITIALS  | MOTIVATIONS
C      --------------------------------------------------------------
C      | 1999/10/21 G.M.    | READ FROM INPUT T16CPO
C      |                    | PROCESSING OPTIONS
C      --------------------------------------------------------------
C
C  2- ROUTINE PARAMETERS:
C    INPUT
C      IPCPO  : POINTER TO THE CPO STRUCTURE             I
C      IPRINT : PRINT LEVEL                              I
C               >=  10 PRINT ACTIVATED
C      MAXMIX : MAXIMUM NUMBER OF MIXTURE PERMITTED      I
C      MNLOCP : MAXIMUM NUMBER OF LOCAL PARAMETERS       I
C      MNCPLP : MAXIMUM NUMBER OF COUPLED  PARAMETERS    I
C      MNPERT : MAXIMUM NUMBER OF PERTURBATION PER       I
C               LOCAL PARAMETERS
C      NALOCP : LOCAL PARAMETER NAMES PERMITTED          C(MNLOCP
C                                                        +MNCPLP)*4
C      IDLCPL : LOCAL PARAMETER ID FOR PERTURBATION      I(2,MNLOCP
C               PARAMETER                                 +MNCPLP)
C      NCMIXS : NUMBER OF CURRENT MIXTURES SAVED         I
C               TOTAL NUMBER OF MIXTURE SAVED
C      NGCCPO : NUMBER OF CPO GROUPS                     I
C      ENECPO : FINAL ENERGY GROUP STRUCTURE             R(NGCCPO+1)
C      NAMMIX : NAME OF MIXTURE                          I(2,MAXMIX)
C               ALL MIXTURE NAMES TO SAVE
C               CONTAINS VARIABLE CHARACTER*6 NAME
C               READ(NAME,'(A4,A2)') (NAMMIX(J,*),J=1,2)
C      MIXRCI : REFERENCE INFORMATION FOR A MIXTURE      I(2+MNLOCP+
C               (O) = 0 NO INFORMATION FOR MIXTURE  MNCPLP,MAXMIX)
C                   > 0 INFORMATION NOT UPDATED
C                   < 0 INFORMATION TO BE UPDATED
C      PARRCI : REFERENCE LOCAL PARAMETERS FOR A         R(MNLOCP,
C               MIXTURE                                    MAXMIX)
C      PARPER : PERTURBATION PARAMETERS FOR A            R(MNPERT,2,
C               MIXTURE                      MNLOCP+MNCPLP,MAXMIX)
C
C  3- ROUTINES CALLED
C    UTILITIES ROUTINES
C      XABORT : ABORT ROUTINE
C      XDRSET : VECTOR INITIALIZATION ROUTINE
C      LCMGET : DATA STRUCTURE READ ROUTINE
C      LCMSIX : DATA STRUCTURE CHANGE DIRECTORY ROUTINE
C
C----
C
      USE GANLIB
      IMPLICIT         NONE
      TYPE(C_PTR)      IPCPO
      INTEGER          IPRINT,MAXMIX,MNLOCP,MNCPLP,MNPERT,
     >                 NCMIXS,NGCCPO
      CHARACTER        NALOCP(MNLOCP+MNCPLP)*4
      INTEGER          IDLCPL(2,MNLOCP+MNCPLP),NAMMIX(2,MAXMIX),
     >                 MIXRCI(2+MNLOCP+MNCPLP,MAXMIX)
      REAL             ENECPO(NGCCPO+1),PARRCI(MNLOCP,MAXMIX),
     >                 PARPER(MNPERT,2,MNLOCP+MNCPLP,MAXMIX)
C----
C  MEMORY ALLOCATION
C----
      INTEGER ,ALLOCATABLE, DIMENSION(:) :: IDSUF,NBPER
      REAL,ALLOCATABLE, DIMENSION(:) :: ENEMIX,LOCALP
C----
C  LOCAL VARIABLES
C----
      INTEGER          IOUT,NPARAM,NTC,ILCMUP,ILCMDN
      CHARACTER        NAMSBR*6
      PARAMETER       (IOUT=6,NPARAM=4,ILCMUP=1,ILCMDN=2,
     >                 NTC=3,NAMSBR='T16MPI')
      CHARACTER        NAMDIR*12
      INTEGER          IPARAM(NPARAM),
     >                 ILOCP,IMIX,IPER,NBPERP,IGR,
     >                 NLPAR,ILPAR,ILOCL,NPERTN,IR
C----
C  GET MIXTURE NAMES
C----
      CALL LCMGET(IPCPO,'MIXTURE-PREF',NAMMIX)
C----
C  GET PERTURBATION SUFFIX NAMES AND COMPARE WITH
C  REFERENCE NAMES
C----
      NPERTN=MNLOCP+MNCPLP
      ALLOCATE(ENEMIX(NGCCPO+1),IDSUF(NPERTN))
      CALL LCMGET(IPCPO,'PERTURB-SUFX',IDSUF)
      DO 100 ILOCP=1,NPERTN
        WRITE(NAMDIR,'(A4)') IDSUF(ILOCP)
        IF(NAMDIR .NE. NALOCP(ILOCP)) CALL XABORT(NAMSBR//
     >  ': INCOHERENT PERTURBATION NAMES')
 100  CONTINUE
      DEALLOCATE(IDSUF)
C----
C  GET NUMBER OF PERTURBATION PER LOCAL PARAMETER PER MIXTURE
C----
      ALLOCATE(NBPER(NPERTN*NCMIXS))
      CALL LCMGET(IPCPO,'PERTURB-NUMB',NBPER)
      IPER=0
      DO 110 IMIX=1,NCMIXS
        DO 111 ILOCP=1,NPERTN
          IPER=IPER+1
          MIXRCI(2+ILOCP,IMIX)=NBPER(IPER)
 111    CONTINUE
 110  CONTINUE
      DEALLOCATE(NBPER)
      ALLOCATE(LOCALP(MNLOCP))
C----
C  GET LOCAL PARAMETERS
C----
      DO 120 IMIX=1,NCMIXS
C----
C  GET REFERENCE MIXTURE PARAMETERS
C----
        WRITE(NAMDIR,'(A4,A2,A6)')
     >  NAMMIX(1,IMIX),NAMMIX(2,IMIX),'RC    '
        CALL LCMSIX(IPCPO,NAMDIR,ILCMUP)
        CALL LCMGET(IPCPO,'PARAM       ',IPARAM)
        IF(IPARAM(1) .NE. NGCCPO) THEN
          CALL XABORT(NAMSBR//
     >    ': INVALID NUMBER OF GROUPS FOR REFERENCE')
        ENDIF
C----
C  TEST ENERGY GROUP STRUCTURE
C----
        CALL LCMGET(IPCPO,'ENERGY      ',ENEMIX)
        DO 121 IGR=1,NGCCPO+1
          IF(ENECPO(IGR) .NE. ENEMIX(IGR)) THEN
            CALL XABORT(NAMSBR//
     >      ': INVALID GROUP STRUCTURE FOR REFERENCE')
          ENDIF
 121    CONTINUE
C----
C  READ LOCAL PARAMETERS AND TRANSFER
C  TO LOCAL TABLE
C----
        MIXRCI(2,IMIX)=IPARAM(4)
        CALL LCMGET(IPCPO,'LOCAL-PARAMS',LOCALP)
        DO 122 ILOCP=1,MNLOCP
          PARRCI(ILOCP,IMIX)=LOCALP(ILOCP)
 122    CONTINUE
        CALL LCMSIX(IPCPO,NAMDIR,ILCMDN)
C----
C  GET PERTURBATIONS PARAMETERS
C----
        DO 130 ILOCP=1,NPERTN
          NBPERP=MIXRCI(2+ILOCP,MAXMIX)
          IF(NBPERP .GT. 0) THEN
            NLPAR=1
            IF(ILOCP .GT. MNLOCP) NLPAR=2
            DO 131 IPER=1,NBPERP
              WRITE(NAMDIR,'(A4,A2,A4,I2)')
     >        NAMMIX(1,IMIX),NAMMIX(2,IMIX),NALOCP(ILOCP),IPER
              CALL LCMSIX(IPCPO,NAMDIR,ILCMUP)
              CALL LCMGET(IPCPO,'PARAM       ',IPARAM)
              IF(IPARAM(1) .NE. NGCCPO) THEN
                CALL XABORT(NAMSBR//
     >          ': INVALID NUMBER OF GROUPS FOR PERTURBATION')
              ENDIF
C----
C  TEST ENERGY GROUP STRUCTURE
C----
              CALL LCMGET(IPCPO,'ENERGY      ',ENEMIX)
              DO 132 IGR=1,NGCCPO+1
                IF(ENECPO(IGR) .NE. ENEMIX(IGR)) THEN
                  CALL XABORT(NAMSBR//
     >            ': INVALID GROUP STRUCTURE FOR REFERENCE')
                ENDIF
 132          CONTINUE
              CALL LCMGET(IPCPO,'LOCAL-PARAMS',LOCALP)
C----
C  READ PERTURBED LOCAL PARAMETERS AND TRANSFER
C  TO LOCAL TABLE
C----
              DO 133 ILPAR=1,NLPAR
                ILOCL=IDLCPL(ILPAR,ILOCP)
                PARPER(IPER,ILPAR,ILOCP,IMIX)=LOCALP(ILOCL)
 133          CONTINUE
              CALL LCMSIX(IPCPO,NAMDIR,ILCMDN)
 131        CONTINUE
          ENDIF
 130    CONTINUE
 120  CONTINUE
      DEALLOCATE(LOCALP,ENEMIX)
C----
C  PROCESS PRINT LEVEL
C----
      IF(IPRINT .GE. 10) THEN
        WRITE(IOUT,6000) NAMSBR,NCMIXS
        DO 600 IMIX=1,NCMIXS
          IF(MIXRCI(2,IMIX) .GT. 0) THEN
            WRITE(IOUT,6010) (NAMMIX(IR,IMIX),IR=1,2),
     >                        MIXRCI(2,IMIX)
            WRITE(IOUT,6020)
     >      (NALOCP(ILOCP),PARRCI(ILOCP,IMIX),ILOCP=1,MNLOCP)
          ENDIF
          DO 610 ILOCP=1,NPERTN
            NBPERP=MIXRCI(2+ILOCP,IMIX)
            NLPAR=1
            IF(ILOCP .GT. MNLOCP) NLPAR=2
            IF(NBPERP .GT. 0) THEN
              WRITE(IOUT,6011) NBPERP,NALOCP(ILOCP)
              DO 611 IPER=1,NBPERP
                WRITE(IOUT,6022) IPER,
     >          (NALOCP(IDLCPL(ILPAR,ILOCP)),
     >           PARPER(IPER,ILPAR,ILOCP,IMIX),
     >           ILPAR=1,NLPAR)
 611          CONTINUE
            ENDIF
 610      CONTINUE
 600    CONTINUE
        WRITE(IOUT,6001)
      ENDIF
      RETURN
C----
C  PRINT FORMAT
C----
 6000 FORMAT(1X,5('*'),'OUTPUT FROM ',A6,5('*')/
     >       6X,'CONTENTS OF CPO BEFORE UPDATE'/
     >       6X,'NUMBER OF MIXTURES  = ',I10)
 6001 FORMAT(1X,28('*'))
 6010 FORMAT(6X,'NAME OF MIXTURES    = ',A4,A4,
     >       2X,'NUMBER OF BURNUP    = ',I10,
     >       2X,'ALREADY STORED ON CPO FILE')
 6011 FORMAT(6X,I10,' PERTURBATIONS FOR ',A4,
     >       2X,'ALREADY STORED ON CPO FILE')
 6020 FORMAT(6X,'LOCAL PARAMETER FOR REFERENCE CASE'/
     >       1P,6(2x,A4,1X,E10.3))
 6022 FORMAT(6X,'LOCAL PARAMETER PERTURBATION = ',I2/
     >       1P,2(2x,A4,1X,E10.3))
      END
