*DECK LIBWRI
      SUBROUTINE LIBWRI(NTMPR,NDILR,TMPISO,DILISO,TMPT,DILT,REST,RIT,
     >                  XSOUT,XSCOR)
C
C-------------------------- LIBWRI   ----------------------------------
C
C  PROGRAMME STATISTICS:
C     NAME     : LIBWRI
C     ENTRY    : LIBWRI
C     USE      : RESONANCE INTEGRAL TEMP AND DIL INTERPOLATION
C     MODIFIED : 93-08-17
C     AUTHOR   : G. MARLEAU
C
C  ROUTINE PARAMETERS:
C   INPUT
C      NTMPR  : NUMBER OF TEMPERATURE TABLES             I
C      NDILR  : NUMBER OF DILUTION TABLES                I
C      TMPISO : TEMPERATURE OF ISOTOPE                   R
C      DILISO : DILUTION OF ISOTOPE                      R
C      TMPT   : SQRT(TEMPERATURE) IN TABLE               R(NTMPR)
C      DILT   : SQRT(DILUTION) IN TABLE                  R(NDILR)
C      REST   : RESONANCE RATES INPUT                    R(NDILR,
C                                                          NTMPR)
C   WORK
C      RIT    : DUMMY VECTRO                             R(NDILR)
C   OUTPUT
C      XSOUT  : OUTPUT RESONANCE INTEGRALS               R
C   INTERNAL PARAMETER
C      EPSRI  : AIKINT INTERPOLATION PARAMETER = 0.0005  R
C      SQDILI : SQRT INFINITE DILUTION = 1.0E+5          R
C
C-------------------------- LIBWRI   ----------------------------------
C
      IMPLICIT   NONE
      REAL       EPSRI,SQDILI
      PARAMETER (EPSRI=0.0005,SQDILI=1.0E+5)
      INTEGER    NTMPR,NDILR
      REAL       TMPISO,DILISO,TMPT(NTMPR),DILT(NDILR),
     >           REST(NDILR,NTMPR),RIT(NDILR),XSOUT,XSCOR
      INTEGER    IDIL,NDILE,JDEPT,JFINT,IR,JD
      REAL       SQTD,SQTT,ALPHA,AIKINT,ASLOPE
C----
C  SIMPLE LINEAR INTERFOLATION IN SQRT(TMP)
C----
      SQTD=SQRT(DILISO)
      XSOUT=0.0
      DO 110 IDIL=NDILR,1,-1
        IF(DILT(IDIL).LT.SQDILI) THEN
          NDILE=IDIL
          GO TO 115
        ENDIF
 110  CONTINUE
      RETURN
 115  CONTINUE
      SQTT=SQRT(TMPISO)
      IF(NTMPR.EQ.1) THEN
        JDEPT=1
        JFINT=1
        ALPHA=0.0
      ELSE
        JDEPT=1
        DO 100 IR=1,NTMPR-1
          IF(SQTT.GE.TMPT(IR)) JDEPT=IR
 100    CONTINUE
        JFINT=JDEPT+1
        ALPHA=(SQTT-TMPT(JDEPT))/(TMPT(JFINT)-TMPT(JDEPT))
      ENDIF
      DO 120 JD=1,NDILR
        RIT(JD)=(1.-ALPHA)*REST(JD,JDEPT)+
     >           ALPHA*REST(JD,JFINT)
 120  CONTINUE
      IF(SQTD .GT. DILT(NDILE)) THEN
C----
C  INTERPOLATE LINEARLY BETWEEN LAST DILUTION IN TABLE
C  AND INFINITE DILUTION
C----
        ASLOPE=(DILT(NDILE)/SQTD)**2
        XSOUT=ASLOPE*RIT(NDILE)+(1.0-ASLOPE)*RIT(NDILR)
      ELSE
C----
C  AIKINT INTERPOLATION FOR DILUTION
C----
        XSOUT=AIKINT(SQTD,DILT,RIT,NDILE,EPSRI)
      ENDIF
      XSCOR=XSCOR+XSOUT
      RETURN
      END
