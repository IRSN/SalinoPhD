*DECK INFWIM
      SUBROUTINE INFWIM(CFILNA,IPRINT,NBISO,HNAMIS,AWR)
C
C------------------------------  INFWIM  ------------------------------
C
C   TO RECOVER MASS FOR ISOTOPES OF WIMS-AECL TYPE LIBRARIES
C
C   INPUT
C     CFILNA : WIMS FILE NAME                       C*64
C     IPRINT : PRINT FLAG                           I
C     NBISO  : NUMBER OF ISOTOPES                   I
C     HNAMIS : ISOTOPE NAMES                        C(NBISO)*8
C   OUTPUT
C     AWR    : ISOTOPE WEIGHTS                      R(NBISO)
C   WIMS-AECL LIBRARY PARAMETERS
C     IOUT   : OUTPUT UNIT = 6                      I
C     IACTO  : OPEN ACTION = 2 (READ ONLY)          I
C     LRIND  : LENGHT RECORD ON DA FILE = 256       I
C     MAXISO : MAX. NB. OF ISO = 246                I
C     NCT    : NUMBER OF C*8 IN TITLE = 10          I
C     LPZ    : LENGTH OF WIMS PARAMETER ARRAY = 9   I
C     LMASTB : LENGTH OF MST TAB = MAXISO+9         I
C     LMASIN : LENGTH OF MST IDX = LMASTB-4         I
C     LGENTB : LENGTH OF GEN TAB = 6                I
C     LGENIN : LENGTH OF GEN IDX = LGENTB           I
C     LSUBTB : LENGTH OF SUB TAB = 6*MAXTEM+21-5+12 I
C     LSUBIN : LENGTH OF SUB IDX = LSUBTB-12        I
C     MASTER : MASTER INDEX ARRAY                   I(LMASTB)
C     GENINX : GENERAL INDEX ARRAY                  I(LGENTB)
C     SUBINX : SUB INDEX ARRAY                      I(LSUBTB)
C     ZUBINX : REAL EQUIVALENT OF SUBINX            R(LSUBTB)
C
C------------------------------  INFWIM  ------------------------------
C
      IMPLICIT   NONE
      INTEGER    IOUT,IUTYPE,IACTO,LRIND,MAXISO,MAXTEM,NCT,
     >           LPZ,LMASTB,LMASIN,LGENTB,LGENIN,LSUBTB,LSUBIN
      PARAMETER (IOUT=6,IUTYPE=4,IACTO=2,LRIND=256,
     >           MAXISO=246,MAXTEM=20,
     >           NCT=10,LPZ=9,LMASTB=MAXISO+9,
     >           LMASIN=LMASTB-4,LGENTB=6,LGENIN=LGENTB,
     >           LSUBTB=6*MAXTEM+28,LSUBIN=LSUBTB-12)
      INTEGER    MASTER(LMASTB),GENINX(LGENTB),SUBINX(LSUBTB),
     >           IWISO(MAXISO),IPRINT,NBISO,NPZ(LPZ),
     >           ITITLE(2*NCT)
      CHARACTER  CFILNA*64,HNAMIS(NBISO)*8,
     >           CWISO(MAXISO)*8,CTITLE(NCT)*8
      REAL       ZUBINX(LSUBTB),AWR(NBISO)
      INTEGER    IUNIT,IRISO,ISO,JSO,II,KDROPN
      EXTERNAL   KDROPN
      EQUIVALENCE (SUBINX(1),ZUBINX(1))
C----
C  ALLOCATABLE ARRAYS
C----
      INTEGER, ALLOCATABLE, DIMENSION(:) :: ICISO
C
      IF( CFILNA.EQ.' ' )THEN
        CALL XABORT('INFWIM: WIMS LIBRARY HAS NOT BEEN SET')
      ENDIF
C----
C  OPEN WIMSLIB AND READ TITLE
C----
      IRISO=0
      IUNIT=KDROPN(CFILNA,IACTO,IUTYPE,LRIND)
      IF(IUNIT.LE.0) CALL XABORT(
     > 'INFWIM: WIMS-AECL LIBRARY CANNOT BE OPENED FOR MIXS :'//CFILNA)
      CALL OPNIND(IUNIT,MASTER,LMASTB)
      CALL REDIND(IUNIT,MASTER,LMASIN,GENINX,LGENTB,1)
      IF(IPRINT.GT.0) THEN
        CALL REDIND(IUNIT,MASTER,LMASIN,ITITLE,2*NCT,2)
        CALL UPCKIC(ITITLE,CTITLE,NCT)
        WRITE(IOUT,6000) CFILNA
        WRITE(IOUT,'(1X,10A8)') (CTITLE(II),II=1,NCT)
      ENDIF
C----
C  READ GENERAL INDEX, ISOTOPES NAMES AND GROUP STRUCTURE
C----
      CALL REDIND(IUNIT,GENINX,LGENIN,NPZ,LPZ,1)
      CALL REDIND(IUNIT,GENINX,LGENIN,IWISO,NPZ(1),2)
      ALLOCATE(ICISO(2*NPZ(1)))
      CALL REDIND(IUNIT,GENINX,LGENIN,ICISO,2*NPZ(1),3)
      CALL UPCKIC(ICISO,CWISO,NPZ(1))
      IF(IPRINT.GE.100) THEN
        WRITE(IOUT,6200) (CWISO(II),II=1,NPZ(1))
      ENDIF
      DEALLOCATE(ICISO)
C----
C READ THROUGH DRAGON FILE AND ACCUMULATE WEIGHTS.
C----
      DO 120 ISO=1,NBISO
        DO 130 JSO=1,NPZ(1)
          IF(CWISO(JSO).EQ.HNAMIS(ISO)) THEN
            IRISO=JSO
            GO TO 131
          ENDIF
 130    CONTINUE
        WRITE(IOUT,9002) HNAMIS(ISO),CFILNA
        CALL XABORT('INFWIM: ISOTOPE NOT FOUND ON LIBRARY')
 131    CONTINUE
        IF(IPRINT.GT.0) THEN
          WRITE(IOUT,6001) HNAMIS(ISO)
        ENDIF
C----
C  READ SUB INDEX ASSOCIATED WITH ISOTOPE
C----
        CALL REDIND(IUNIT,MASTER,LMASIN,SUBINX,LSUBTB,IRISO+4)
        AWR(ISO)=ZUBINX(LSUBIN+3)
 120  CONTINUE
      CALL CLSIND(IUNIT)
C----
C  RETURN
C----
      RETURN
C----
C  FORMAT
C----
 9002 FORMAT(/' INFWIM: MATERIAL/ISOTOPE ',A8,' IS MISSING ON WIMS',
     >        ' FILE NAME ',A64)
 6000 FORMAT(/' PROCESSING WIMS LIBRARY NAME ',A64)
 6001 FORMAT(/'    PROCESSING ISOTOPE/MATERIAL = ',A12)
 6200 FORMAT(1X,'ISOTOPES ON LIBRARY'/6(4X,A8))
      END
