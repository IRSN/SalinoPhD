*DECK XELPSI
      FUNCTION XELPSI(ITYP,RANN2,XYPOS,XYPOS2,SPXY)
C
C--------------------------    XELPSI    -------------------------------
C
C 1- PROGRAMME STATISTICS:
C           NAME: XELPSI
C      COMPONENT: EXCELL GEOMETRY ANALYSIS
C      CALLED BY: XELCRN
C        CALLING: ------
C        VERSION: 1.0
C       CREATION: 97/10/31
C         AUTHOR: G. MARLEAU
c            USE: COMPUTE INTERSECTION SURFACE BETWEEN
C                 PART OF ANNULAR REGION TO THE LEFT OF X-PLANE
C                 AND EITHER THE PART OF THE ANNULAR REGION
C                 ABOVE Y-PLANER THE PART BELOW THE Y-PLANE
C 2- INPUT
C   ITYP   : TYPE OF CALCULATION                            I
C            =1 -> ABOVE Y-PLANE
C            =2 -> BELOW Y-PLANE
C   RANN2  : PIN RADIUS SQUARED                             D
C   XYPOS  : CARTESIAN PLANE LOCATION                       D(2,2)
C            (1,1)  LEFT   X-PLANE
C            (1,2)  RIGHT  X-PLANE
C            (2,1)  BOTTOM Y-PLANE
C            (2,2)  TOP    Y-PLANE
C   XYPOS2 : CARTESIAN MESH SQUARED                         D(2,2)
C            SAME NOTATION AS ABOVE
C   SPXY   : ANNULAR SURFACE OUTSIDE OF PLANES              D(2,2)
C            SAME NOTATION AS ABOVE
C 3- OUTPUT
C   XELPSI : INTERSECTION SURFACE                           D
C 5- INTERNAL PARAMETERS
C   PI     : VALUE OF PI = 3.14159265358979323846           D
C
C--------------------------    XELPSI    -------------------------------
C
      IMPLICIT     NONE
      INTEGER      ITYP
      DOUBLE PRECISION XELPSI,RANN2,XYPOS(2,2),XYPOS2(2,2),SPXY(2,2)
C----
C  LOCAL PARAMETERS
C----
      DOUBLE PRECISION   PI,DZERO
      PARAMETER         (PI=3.14159265358979323846D0,DZERO=0.0D0)
      DOUBLE PRECISION   SQANN,YFC,XFC
C----
C  TEST IF POINT OF INTEREST IS LOCATED INSIDE
C  ANNULAR REGION
C----
      XELPSI=0.0D0
      IF(XYPOS2(2,ITYP)+XYPOS2(1,1).LT.RANN2) THEN
C----
C  FOR POINT INSIDE ANNULAR REGION
C  1) (SUM OF ANNULAR INTERSECTION SURFACES)/2
C     -INTERSECTION SURFACE
C     +(INTERNAL REGION CARTESIAN SURFACE)/4
C     =(ANNULAR SURFACE)/4
C----
        SQANN=0.25D0*PI*RANN2
        YFC=-XYPOS(2,ITYP)
        IF(ITYP.EQ.1) THEN
          XFC=-XYPOS(1,1)
        ELSE
          XFC=XYPOS(1,1)
        ENDIF
        XELPSI=0.5D0*(SPXY(1,1)+SPXY(2,ITYP))+XFC*YFC-SQANN
      ELSE
        IF(ITYP.EQ.1) THEN
          IF(XYPOS(2,ITYP).LT.DZERO.AND.XYPOS(1,1).LT.DZERO) THEN
            XELPSI=DZERO
          ELSE IF(XYPOS(2,ITYP).GT.DZERO.AND.XYPOS(1,1).GT.DZERO) THEN
            XELPSI=SPXY(2,ITYP)+SPXY(1,1)-PI*RANN2
          ELSE IF(XYPOS(2,ITYP).GT.DZERO.AND.XYPOS(1,1).LT.DZERO) THEN
            XELPSI=SPXY(1,1)
          ELSE IF(XYPOS(2,ITYP).LT.DZERO.AND.XYPOS(1,1).GT.DZERO) THEN
            XELPSI=SPXY(2,ITYP)
          ENDIF
        ELSE
          IF(XYPOS(2,ITYP).LT.DZERO.AND.XYPOS(1,1).LT.DZERO) THEN
            XELPSI=SPXY(1,1)
          ELSE IF(XYPOS(2,ITYP).GT.DZERO.AND.XYPOS(1,1).GT.DZERO) THEN
            XELPSI=SPXY(2,ITYP)
          ELSE IF(XYPOS(2,ITYP).GT.DZERO.AND.XYPOS(1,1).LT.DZERO) THEN
            XELPSI=DZERO
          ELSE IF(XYPOS(2,ITYP).LT.DZERO.AND.XYPOS(1,1).GT.DZERO) THEN
            XELPSI=SPXY(2,ITYP)+SPXY(1,1)-PI*RANN2
          ENDIF
        ENDIF
      ENDIF
      RETURN
      END
