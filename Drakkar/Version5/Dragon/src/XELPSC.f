*DECK XELPSC
      FUNCTION XELPSC(RANN,PLANE)
C
C--------------------------    XELPSC    -------------------------------
C
C 1- PROGRAMME STATISTICS:
C           NAME: XELPSC
C      COMPONENT: EXCELL GEOMETRY ANALYSIS
C      CALLED BY: XELCRN
C        CALLING: ------
C        VERSION: 1.0
C       CREATION: 97/10/31
C         AUTHOR: G. MARLEAU
C            USE: COMPUTE ANNULAR SURFACE BELOW CARTESIAN PLANE
C 2- INPUT
C   RANN   : ANNULAR RADIUS                                 D
C   PLANE  : CARTESIAN PLANE LOCATION                       D
C 3- OUTPUT
C   XELPSC : ALLULAR SURFACE BELOW PLANE                    D
C
C--------------------------    XELPSC    -------------------------------
C
      IMPLICIT          NONE
      DOUBLE PRECISION  XELPSC,RANN,PLANE
C----
C  LOCAL VARIABLES
C----
      DOUBLE PRECISION  RANN2,PLANE2,ALPHA
C----
C  METHOD
C  1) FIND HALF-ANGLE COVERED BY THE TWO INTERSECTION POINTS
C     BETWEEN PLANE AND ANNULAR REGION
C     -- ALPHA=ACOS(-PLANE/RANN)
C  2) COMPUTED ANNULAR SURFACE COVERED BY THIS HALF-ANGLE
C     -- 0.5*RANN2*ALPHA
C  3) ADD SURFACE COVERED BY INTERNAL RECTANGLE IN THIS HALF-ANGLE
C     -- 0.5*PLANE*SQRT(RANN2-PLANE2)
C  4) DOULBLE SURFACE FOR FULL ANGLE
C----
      RANN2=RANN*RANN
      PLANE2=PLANE*PLANE
      ALPHA=ACOS(-PLANE/RANN)
      XELPSC=RANN2*ALPHA+PLANE*SQRT(RANN2-PLANE2)
      RETURN
      END
