*DECK XELCRN
      SUBROUTINE XELCRN(IPRINT,RANN2,NRSPX,NRSPY,SPAT,AREAI)
C
C--------------------------    XELCRN    -------------------------------
C
C 1- PROGRAMME STATISTICS:
C           NAME: XELCRN
C      COMPONENT: EXLELL GEOMETRY ANALYSIS
C      CALLED BY: XELVOL
C        CALLING: XELPSC, XELPSI
C        VERSION: 1.0
C       CREATION: 97/10/31
C         AUTHOR: G. MARLEAU
C            USE: FIND 2-D SURFACE OF INTERSECTION BETWEEN
C                 ANNULAR REGION AND CARTESIAN PLANE
C 2- INPUT
C   IPRINT : PRINT LEVEL                            I
C            ACTIVE IF >=10
C   RANN2  : ANNULAR REGION  RADIUS**2              D
C   NRSPX  : NUMBER OF MESH IN X- DIRECTION         I
C   NRSPY  : NUMBER OF MESH IN X- DIRECTION         I
C   SPAT   : SPATIAL MESH X-DIRECTION               D(NRSPX+1,NRSPY+1)
C            SPAT(1,1)       = LOWER X - POSITION
C            SPAT(NRSPX+1,1) = UPPER X - POSITION
C            SPAT(1,2)       = LOWER Y - POSITION
C            SPAT(NRSPY+1,2) = UPPER Y - POSITION
C 3- OUTPUT
C   AREAI  : AREA OF INTERSECTION                   D(NRSPX,NRSPY)
C 5- INTERNAL PARAMETERS
C   IOUT   : OUTPUT UNIT  = 6                       I
C   PI     : VALUE OF PI = 3.14159265358979323846   D
C   NAMSBR : SUBROUTINE NAME = 'XELCRN'             C*6
C
C--------------------------    XELCRN    -------------------------------
C
      IMPLICIT          NONE
      INTEGER           IPRINT,NRSPX,NRSPY
      DOUBLE PRECISION  RANN2,SPAT(NRSPX+1,NRSPY+1),
     >                  AREAI(NRSPX,NRSPY)
C----
C  INTERNAL PARAMETERS
C----
      INTEGER      IOUT
      CHARACTER    NAMSBR*6
      PARAMETER   (IOUT=6,NAMSBR='XELCRN')
      DOUBLE PRECISION   PI,DZERO
      PARAMETER   (PI=3.14159265358979323846D0,DZERO=0.0D0)
C----
C  LOCAL VARIABLES
C----
      INTEGER          IRP(2,2),IX,NMX,IY,NMY
      DOUBLE PRECISION XELPSC,XELPSI,XYPOS(2,2),XYPOS2(2,2),
     >                 SPXY(2,2),SIXY(2,2),RANN,SURANN 
C----
C  COMPUTE GENERAL ANNULAR REGION INFORMATIONS
C    RANN   = ANNULAR REGION RADIUS
C    SURANN = ANNULAR SURFACE
C  COMPUTE CARTESIAN PARAMETERS
C    NMX =NRSPX+1
C    NMY =NRSPY+1
C  INITIALIZE AREAI TO 0.0D0
C----
      RANN=SQRT(RANN2)
      SURANN=PI*RANN2
      NMX=NRSPX+1
      NMY=NRSPY+1
      CALL XDDSET(AREAI,NRSPX*NRSPY,DZERO)
      CALL XDISET(IRP,4,0)
C----
C  PRINT INITIAL MESH IF REQUIRED
C-----
      IF(IPRINT .GE. 10) THEN
        WRITE(IOUT,6000)
        WRITE(IOUT,6002) 'ANNULAR RADIUS  '
        WRITE(IOUT,6003) RANN
        WRITE(IOUT,6002) 'ANNULAR SURFACE '
        WRITE(IOUT,6003) SURANN
        WRITE(IOUT,6002) 'X-DIRECTED MESH '
        WRITE(IOUT,6003) (SPAT(IX,1),IX=1,NRSPX+1)
        WRITE(IOUT,6002) 'Y-DIRECTED MESH '
        WRITE(IOUT,6003) (SPAT(IY,2),IY=1,NRSPY+1)
        WRITE(IOUT,6002) 'X-Y SURFACES    '
        WRITE(IOUT,6003) (( (SPAT(IX+1,1)-SPAT(IX,1))
     >                     *(SPAT(IY+1,2)-SPAT(IY,2)),
     >                     IX=1,NRSPX),IY=1,NRSPY)
      ENDIF
C----
C  CYCLE OVER CARTESIAN Y-DIRECTIONS STARTING FROM THE END
C  AND LOCATE Y-MESH POSITION WITH RESPECT TO ANNULUS CENTER
C----
      SPXY(2,2)=DZERO
      DO 110 IY=NMY,1,-1
        XYPOS(2,1)=SPAT(IY,2)
        XYPOS2(2,1)=XYPOS(2,1)*XYPOS(2,1)
C----
C  FIND IF ANNULUS ABOVE, BELOW OR INTERSECT CURRENT Y-PLANE
C  AND COMPUTE
C    SPXY = ANNULAR SURFACE BELOW CURRENT PLANE
C  IF ANNULUS BELOW CURRENT PLANE (XYPOS(2,1)>= RANN)
C    IRPY(2,1)=-1
C    SPXY(2,1)=SURANN
C  IF ANNULUS ABOVE CURRENT PLANE (XYPOS(2,1)<= -RANN)
C    IRPY(2,1)= 1
C    SPXY(2,1)=0.0
C  IF ANNULUS INTERSECT CURRENT ( -RANN < XYPOS(2,1) < RANN)
C    IRPY(2,1)= 0
C    SPXY=XELPSC(RANN,XYPOS(2,1))
C----
        IF(XYPOS(2,1) .GE. RANN) THEN
          IRP(2,1)=-1
          SPXY(2,1)=SURANN
        ELSE IF(XYPOS(2,1) .LE. -RANN) THEN
          IRP(2,1)=1
          SPXY(2,1)=DZERO
        ELSE
          IRP(2,1)=0
          SPXY(2,1)=XELPSC(RANN,XYPOS(2,1))
        ENDIF
C----
C  FOR LAST PLANE IN Y DIRECTION OR
C  Y-PLANE ABOVE ANNULAR VOLUME
C  GO TO LABEL 111
C----
        IF(IY .EQ. NMY .OR. IRP(2,1) .EQ. -1) GO TO 111
C----
C  CYCLE OVER CARTESIAN X-DIRECTIONS STARTING FROM THE END
C  AND LOCATE X-MESH POSITION WITH RESPECT TO ANN CENTER
C----
        SPXY(1,2)=DZERO
        SIXY(2,1)=DZERO
        SIXY(2,2)=DZERO
        DO 120 IX=NMX,1,-1
          XYPOS(1,1)=SPAT(IX,1)
          XYPOS2(1,1)=XYPOS(1,1)*XYPOS(1,1)
C----
C  FIND IF ANNULUS LEFT, RIGHT OR INTERSECT CURRENT X-PLANE
C  AND COMPUTE
C    SPXY      THE  ANNULAR SURFACE LEFT OF CURRENT PLANE
C    SIXY(1,1) THE INTERSECTION BETWEEN THE PART OF THE ANNULUS
C              THE LEFT OF X-PLANE
C              AND THE PART OF THE ANNULUS AT
C              THE BOTTOM OF CURRENT Y-PLANE
C    SIXY(1,2) THE INTERSECTION BETWEEN THE PART OF THE ANNULUS
C              THE LEFT OF X-PLANE
C              AND THE PART OF THE ANNULUS AT
C              THE TOP OF PREVIOUS Y-PLANE
C  IF ANNULUS TO THE LEFT OF CURRENT PLANE (XYPOS(1,1)>= RANN)
C    IRPY(1,1)=-1
C    SPXY(1,1)=SURANN
C    SIXY(1,1)=SPXY(2,1)
C    SIXY(1,2)=SPXY(2,2)
C  IF ANNULUS TO THE RIGHT OF CURRENT (XYPOS(1,1)<= -RANN)
C    IRPY(1,1)= 1
C    SPXY(1,1)=0.0
C    SIXY(1,1)=0.0
C    SIXY(1,2)=0.0
C  IF ANNULUS INTERSECT CURRENT PLANE ( -RANN < XYPOS(1,1) < RANN)
C    IRPY(1,1)= 0
C    SPXY=XELPSC(RANN,XYPOS(1,1))
C    SIXY(1,1)=GEOPSI(1,RANN2,XYPOS,XYPOS2,SPXY)
C    SIXY(1,2)=GEOPSI(2,RANN2,XYPOS,XYPOS2,SPXY)
C----
          SPXY(1,1)=DZERO
          SIXY(1,1)=DZERO
          SIXY(1,2)=DZERO
          IF(XYPOS(1,1) .GE. RANN) THEN
            IRP(1,1)=-1
            SPXY(1,1)=SURANN
            SIXY(1,1)=SPXY(2,1)
            SIXY(1,2)=SPXY(2,2)
          ELSE IF(XYPOS(1,1) .LE. -RANN) THEN
            IRP(1,1)=1
          ELSE
            IRP(1,1)=0
            SPXY(1,1)=XELPSC(RANN,XYPOS(1,1))
            IF(IRP(2,1) .EQ. 0)
     >        SIXY(1,1)=XELPSI(1,RANN2,XYPOS,XYPOS2,SPXY)
            IF(IRP(2,2) .EQ. 0)
     >        SIXY(1,2)=XELPSI(2,RANN2,XYPOS,XYPOS2,SPXY)
          ENDIF
C----
C  FOR LAST PLANE IN X DIRECTION OR
C  X-PLANE TO THE RIGHT OF ANNULAR VOLUME
C  GO TO LABEL 121
C----
          IF(IX .EQ. NMX .OR. IRP(1,1) .EQ. -1) GO TO 121
C----
C  GET SURFACE INTERSECTION BETWEEN ANNULUS AND CARTESIAN REGION
C  LOCATED BETWEEN X-PLANES (IX-> IX+1) AND Y-PLANES (IX -> IY+1)
C  AND STORE IN AREAI(IX,IY)
C----
            AREAI(IX,IY)=SURANN
     >       -SPXY(1,1)-SPXY(1,2)-SPXY(2,1)-SPXY(2,2)
     >       +SIXY(1,1)+SIXY(2,1)+SIXY(1,2)+SIXY(2,2)
C----
C  WHEN ANNULUS ALL LOCATED TO THE RIGHT OF CURRENT X-PLANE
C  EXIT FROM IX LOOP BY GOING TO LABLE 122
C---
          IF(IRP(1,1) .EQ. 1) GO TO 122
 121      CONTINUE
C----
C  RESET IN LOCATION 2 VALUES COMPUTED WITH LOCATION 1
C  WITH ADEQUATE CHANGE OF SIGN FOR SURFACE DIRECTION
C  NAMELY SURFACES LOCATED ON THE LEFT OF X-PLANE BECOME SURFACES
C  LOCATED ON THE RIGHT OF X-PLANE
C----
          SIXY(2,1)=SPXY(2,1)-SIXY(1,1)
          SIXY(2,2)=SPXY(2,2)-SIXY(1,2)
          XYPOS(1,2)=XYPOS(1,1)
          XYPOS2(1,2)=XYPOS2(1,1)
          IRP(1,2)=-IRP(1,1)
          SPXY(1,2)=SURANN-SPXY(1,1)
 120    CONTINUE
 122    CONTINUE
C----
C  WHEN ANNULUS ALL LOCATED ABOVE CURRENT Y-PLANE
C  EXIT FROM IY LOOP BY GOING TO LABLE 112
C---
        IF(IRP(2,1) .EQ. 1) GO TO 112
 111    CONTINUE
C----
C  RESET IN LOCATION 2 VALUES COMPUTED WITH LOCATION 1
C  WITH ADEQUATE CHANGE OF SIGN FOR SURFACE DIRECTION
C  NAMELY SURFACES LOCATED ON THE BELOW Y-PLANE BECOME SURFACES
C  LOCATED ABOVE Y-PLANE
C----
        XYPOS(2,2)=XYPOS(2,1)
        XYPOS2(2,2)=XYPOS2(2,1)
        IRP(2,2)=-IRP(2,1)
        SPXY(2,2)=SURANN-SPXY(2,1)
 110  CONTINUE
 112  CONTINUE
C----
C  PRINT SURFACE INTERSECTIONS IF REQUIRED
C-----
      IF(IPRINT.GE.10) THEN
        WRITE(IOUT,6002) 'CART-ANN AREA   '
        WRITE(IOUT,6003) ((AREAI(IX,IY),IX=1,NRSPX),IY=1,NRSPY)
        WRITE(IOUT,6001)
      ENDIF
C----
C  RETURN
C----
      RETURN
C----
C  PRINT FORMAT
C----
 6000 FORMAT(/5X,'------ OUTPUT FROM XELCRN ------ ')
 6001 FORMAT(5X,' -------------------------------- '/)
 6002 FORMAT(5X,A16)
 6003 FORMAT(1P,5E16.6)
      END
