*DECK AXGGEO
      SUBROUTINE AXGGEO(IPGEOM,IPTRKM,IPRINT,GEONAM)
C
C----
C  1- PROGRAMME STATISTICS:
C      NAME     : AXGGEO
C      USE      : GENERATE TEMPORARY TRACKING FILE
C                 TO BE USED BY PSPTRK
C      AUTHOR   : G.MARLEAU
C      CREATED  : 2001/10/30
C
C
C  2- ROUTINE PARAMETERS:
C     IPGEOM : GEOMETRY DATA STRUCTURES POINTER
C              ***> INTEGER IPGEOM
C     IPTRKM : TRACKING DATA STRUCTURES POINTER         
C              ***> INTEGER IPTRKM
C     IPRINT : PRINT LEVEL                              
C              ***> INTEGER IPRINT
C     GEONAM : GEOMETRY NAME
C              ***> CHARACTER*12 GEONAM
C----
C
      USE          GANLIB
      IMPLICIT     NONE
      INTEGER      IOUT,NSTATE
      CHARACTER    NAMSBR*6
      PARAMETER   (IOUT=6,NSTATE=40,
     >             NAMSBR='AXGGEO')
C----
C  ROUTINE PARAMETERS
C----
      TYPE(C_PTR)  IPGEOM,IPTRKM
      INTEGER      IPRINT
      CHARACTER    GEONAM*12
C----
C  LOCAL PARAMETERS
C---- 
      INTEGER     ISTATE(NSTATE)
      INTEGER     ITYPEG,ITGEO
      CHARACTER   HSIGN*12
      INTEGER     NV,NS,NSOUT,NREG,NUNK,ICODE(6)
      REAL        EXTKOP(NSTATE)
      INTEGER     ITROP,MAXMIX,IREG,ISYMM
      INTEGER     IUEXP,KDROPN,KDRCLS,IRC 
C----
C  ALLOCATABLE ARRAYS
C----
      INTEGER, ALLOCATABLE, DIMENSION(:) :: KEYMRG,MATALB,MATMRG
      REAL, ALLOCATABLE, DIMENSION(:) :: VOLSUR,VOLMRG
C----
C  STORE SIGNATURE AND TRACK TYPE ON IPTRKM
C----
      HSIGN='L_TRACK     '
      CALL LCMPTC(IPTRKM,'SIGNATURE',12,1,HSIGN)
      HSIGN='EXCELL      '
      CALL LCMPTC(IPTRKM,'TRACK-TYPE',12,1,HSIGN)
C----
C  ANALYZE GEOMETRY
C----
      CALL XDISET(ISTATE,NSTATE,0)
      CALL LCMGET(IPGEOM,'STATE-VECTOR',ISTATE)
      ITYPEG= ISTATE(1)
      ITROP = 0
      IF(ITYPEG .EQ. 3 .OR. ITYPEG .EQ. 6 ) THEN
        ITGEO= 1
      ELSE IF(ITYPEG .EQ.  8 .OR. ITYPEG .EQ.  9 .OR.
     >        ITYPEG .EQ. 24 .OR. ITYPEG .EQ. 25      ) THEN
        ITGEO= 2
      ELSE IF(ITYPEG .EQ.  5 .OR. ITYPEG .EQ.  7 .OR.
     >        ITYPEG .EQ. 20 .OR. ITYPEG .EQ. 21 .OR.
     >        ITYPEG .EQ. 22 .OR. ITYPEG .EQ. 23      ) THEN
        ITGEO= 3
      ELSE
        ITGEO= 0
      ENDIF
      IF(ISTATE(13) .GE. 1) THEN
C----
C  CLUSTER GEOMETRY
C----     
        ISYMM=1
        CALL AXGXCW(IPGEOM ,IPTRKM,IPRINT,GEONAM,ISYMM )
        ITROP=3
      ELSE IF(ITGEO .EQ. 2 ) THEN
C----
C  HEXAGONAL 2D GEOMETRIES
C----
C          CALL AXGXHX(IPGEOM ,IPTRKM,IPRINT,GEONAM)
        ITROP=2
      ELSE IF(ITGEO .EQ. 3 ) THEN
C----
C  CARTESIAN 2D/3D ASSEMBLIES
C  CALL XELPRP TO GET GEOMETRY DIMENSIONING INFORMATION
C----
        CALL AXGXEL(IPGEOM ,IPTRKM,IPRINT,GEONAM)
        ITROP=1
      ELSE
        CALL XABORT(NAMSBR//': INVALID TYPE OF GEOMETRY')
      ENDIF
      CALL LCMGET(IPTRKM,'ICODE       ',ICODE)
      CALL LCMSIX(IPTRKM,'EXCELL      ',1)
      CALL XDISET(ISTATE,NSTATE,0)
      CALL LCMGET(IPTRKM,'STATE-VECTOR',ISTATE) 
      NV=ISTATE(3)
      NS=ISTATE(2)
      NUNK=NV+NS+1
      ALLOCATE(KEYMRG(NUNK),MATALB(NUNK),VOLSUR(NUNK))
      CALL LCMGET(IPTRKM,'KEYMRG      ',KEYMRG)
      CALL LCMGET(IPTRKM,'MATALB      ',MATALB)
      CALL LCMGET(IPTRKM,'VOLSUR      ',VOLSUR)
      CALL LCMSIX(IPTRKM,'EXCELL      ',2)
      ALLOCATE(MATMRG(NUNK),VOLMRG(NUNK))
      CALL XELCMP(NS    ,NV    ,
     >            VOLSUR,MATALB,KEYMRG,
     >            NSOUT ,NREG  ,VOLMRG,MATMRG,
     >            ITGEO ,ICODE )
      MAXMIX=0
      DO 100 IREG=1,NREG
        KEYMRG(IREG+NSOUT+1)= IREG
        MAXMIX=MAX(MAXMIX,MATMRG(IREG+NSOUT+1))
 100  CONTINUE
      CALL LCMPUT(IPTRKM,'MATCOD',NREG,1,MATMRG(NSOUT+2))
      CALL LCMPUT(IPTRKM,'VOLUME',NREG,2,VOLMRG(NSOUT+2))
      CALL LCMPUT(IPTRKM,'KEYFLX',NREG,1,KEYMRG(NSOUT+2))
      CALL XDRSET(EXTKOP,NSTATE,0.0)
      CALL LCMPUT(IPTRKM,'EXCELTRACKOP',NSTATE,2,EXTKOP)
      CALL XDISET(ISTATE,NSTATE,0)
      ISTATE(1)=NREG
      ISTATE(2)=NREG
      ISTATE(4)=MAXMIX
      ISTATE(5)=NSOUT
      ISTATE(7)=ITROP
      ISTATE(8)=-1
      CALL LCMPUT(IPTRKM,'STATE-VECTOR',NSTATE,1,ISTATE)
      DEALLOCATE(VOLMRG,MATMRG,VOLSUR,MATALB,KEYMRG)
C----
C  IF IPRINT >= 20
C  EXPORT TEMPORARY TRACKING FILE
C----
      IF(IPRINT .GE. 10) THEN 
        IUEXP=KDROPN('AXGGEOEXPTRK',0,3,0,0)
        CALL LCMEXP(IPTRKM,IPRINT,IUEXP,2,1)
        IRC=KDRCLS(IUEXP,1)
      ENDIF
      RETURN
      END
