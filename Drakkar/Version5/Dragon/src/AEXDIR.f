*DECK AEXDIR
      SUBROUTINE AEXDIR (NFICH,LBLOC,DATA,IADRES,LGSEG)
*
*-----------------------------------------------------------------------
*
* READ INFOMATION FROM A DIRECT ACCESS FILE. COMPONENT OF A FORTRAN-77
* EMULATOR OF THE SAPHYR ARCHIVE SYSTEM.
*
* INPUT PARAMETERS:
*  NFICH  : UNIT NUMBER OF THE DA FILE.
*  LBLOC  : DIRECT ACCESS BUFFER LENGTH.
*  IADRES : OFFSET, FROM START OF FILE WHERE DATA IS EXTRACTED FROM
*           OR WHERE DATA IS TO BE STORED.
*  LGSEG  : NUMBER OF WORDS TO READ FROM OR WRITE INTO FILE.
*
* OUTPUT PARAMETER:
*  DATA   : ADDR. IN MEMORY WHERE DATA IS TO BE MOVED OR EXTRACTED.
*           DIMENSION DATA(LGSEG)
*
*----------------------------------- AUTHOR: A. HEBERT ; 13/09/1999 ----
*
      IMPLICIT INTEGER(A-Z)
      INTEGER DATA(LGSEG),LNEWAD(2)
      INTEGER, ALLOCATABLE, DIMENSION(:) :: WRK
*
      ALLOCATE(WRK(LBLOC))
      INDEX=IADRES
      ID=0
      NROLD=0
   10 NREC=1+INDEX/LBLOC
      N=MOD(INDEX,LBLOC)
      LMIN=1
   20 IF(NREC.NE.NROLD) THEN
*       --------------------------------------------------------
        READ(NFICH,REC=NREC,ERR=90,IOSTAT=IR) (WRK(I),I=1,LBLOC)
*       --------------------------------------------------------
        NROLD=NREC
      ENDIF
      NGRO=MIN(LBLOC+LMIN-N-1,2)
      DO 30 L=LMIN,NGRO
      N=N+1
   30 LNEWAD(L)=WRK(N)
      IF(NGRO.EQ.2) GO TO 40
      NREC=NREC+1
      N=0
      LMIN=NGRO+1
      GO TO 20
   40 LINFO=LNEWAD(2)
      IF(ID+LINFO.GT.LGSEG) CALL XABORT('AEXDIR: DIRECT ACCESS READ FA'
     1 //'ILURE(1).')
      NREC=1+(INDEX+2)/LBLOC
      N=MOD(INDEX+2,LBLOC)
      LMIN=1
   50 IF(NREC.NE.NROLD) THEN
*       --------------------------------------------------------
        READ(NFICH,REC=NREC,ERR=90,IOSTAT=IR) (WRK(I),I=1,LBLOC)
*       --------------------------------------------------------
        NROLD=NREC
      ENDIF
      NGRO=MIN(LBLOC+LMIN-N-1,LINFO)
      DO 60 L=LMIN,NGRO
      N=N+1
   60 DATA(ID+L)=WRK(N)
      IF(NGRO.EQ.LINFO) GO TO 70
      NREC=NREC+1
      N=0
      LMIN=NGRO+1
      GO TO 50
*
   70 INDEX=LNEWAD(1)
      ID=ID+LNEWAD(2)
      IF(ID.EQ.LGSEG) GO TO 80
      GO TO 10
   80 DEALLOCATE(WRK)
      IF(LNEWAD(1).NE.-1) CALL XABORT('AEXDIR: DIRECT ACCESS READ FAIL'
     1 //'URE(3).')
      RETURN
   90 CALL XABORT('AEXDIR: DIRECT ACCESS READ FAILURE(2).')
      END
