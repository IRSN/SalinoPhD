* Author : V. Salino (IRSN), 02/2021
*----
*  Definition STRUCTURES, MODULES et PROCEDURES
*----
PARAMETER PowerCD PowerD PowerARO
          REFL UOX195 UOX310 UOX255_Py8 UOX255_Py12 UOX310_Py12 ::
  ::: XSM_FILE
          REFL UOX195 UOX310 UOX255_Py8 UOX255_Py12 UOX310_Py12 ; ;
LINKED_LIST GeoRes Track System Flux Matex MacroTot Fmap MicroFuel
            GeoCoeur Thermo List ;
MODULE TRIVAT: TRIVAA: FLUD: GREP:  DELETE: END: FLPOW: RESINI: THM: ;
PROCEDURE   Interp GetTechData GeoCore ;

*----
*  Choix de modélisation réflecteur :
*  - TousPaliers
*  - MargCpy
*  - MargCpyAdjus
*----
STRING TypeRefl := "TousPaliers" ;
STRING CondLimite ;
*----
* Si et seulement si Touspaliers est selectionne =
* Palier :
* - CP0_900
* MethodRefl :
* - Lefebvre-Leb
* - Koebke-a
*----
STRING Palier := "CP0_900" ;
STRING MethodRefl := "Lefebvre-Leb" ;

ECHO "TypeRefl =" TypeRefl ;
ECHO "Palier =" Palier ;
ECHO "MethodRefl =" MethodRefl ;

*----
*  Description de la géométrie
*----
REAL dx ;
GeoCoeur GeoRes Matex Fmap := GeoCore :: >>dx<< ;

*----
*  Variables for thermalhydraulics
*----

REAL FlowRate := 21800. (* m3/h *) 3.0 * ;
REAL Tinlet OpePressure dmod tfuel ;
REAL Prel Ptot ;
REAL Pnom := 2652.0 (* MWth *) ;

REAL R_Tub_OD := 0.694499E-2  ; ! m
REAL R_Pin_OD := 0.53677E-2   ; ! m
REAL R_Pin_ID := 0.474956E-2  ; ! m
REAL R_Pel    := 0.465793E-2  ; ! m

REAL NbAss := 157.0 ;
REAL Tot_tub  := R_Tub_OD 2.0 ** $Pi_R * 21.0  * ; ! m2
REAL Tot_pin  := R_Pin_OD 2.0 ** $Pi_R * 204.0 * ; ! m2
REAL sass     := dx 100.0 / 2.0 ** ;               ! m2
REAL asssect  := sass Tot_tub - Tot_pin - ;        ! m2
REAL coresect := NbAss asssect * ;                 ! m2

List := GetTechData :: 'TIH' 'UOX' 0 'None' 'DEPLPARAMAVG' ;
GREP: List :: GETVAL 'dmod'  1 >>dmod<< ;
GREP: List :: GETVAL 'tmod'  1 >>Tinlet<< ;
GREP: List :: GETVAL 'tfuel' 1 >>tfuel<< ;
EVALUATE tfuel := tfuel 273.15 + ;
List := DELETE: List ;
List := GetTechData :: 'TIH' 'UOX' 0 'None' 'GENERAL' ;
GREP: List :: GETVAL 'OpePressure' 1 >>OpePressure<< ;
List := DELETE: List ;

*----
*  Variables used for hot zero power physics testing
*  for 3 different rod insertions
*----
LOGICAL Init := $True_L ;
INTEGER iGrappe ;
REAL CB C D ;
* Index for control rod banks C and D
* - 1.0 for totally unroded
* - 2.0 for insertion of 24 AIC control rods

EVALUATE iGrappe := 1 ;
WHILE iGrappe 3 <= DO
  IF iGrappe 1 = THEN ! C and D banks inserted
    EVALUATE CB C D := 960.0 2.0 2.0 ;
  ELSEIF iGrappe 2 = THEN ! D bank inserted
    EVALUATE CB C D := 1084.0 1.0 2.0 ;
  ELSEIF iGrappe 3 = THEN ! ARO
    EVALUATE CB C D := 1206.0 1.0 1.0 ;
  ENDIF ;

  IF Init THEN
    Fmap := RESINI: Fmap :: SET-PARAM 'C-BORE' <<CB>>
                            SET-PARAM 'D-COOL' <<dmod>>
                            SET-PARAM 'T-FUEL' <<tfuel>> ;
    EVALUATE Init := $False_L ;
  ELSE
    Fmap := RESINI: Fmap :: SET-PARAM 'C-BORE' <<CB>>
                            SET-PARAM 'D-COOL' SAME <<dmod>>
                            SET-PARAM 'T-FUEL' SAME <<tfuel>> ;
  ENDIF ;

  MicroFuel MacroTot Matex Fmap := Interp Matex Fmap
    REFL UOX195 UOX310 UOX255_Py8 UOX255_Py12 UOX310_Py12 ::
    <<CB>> <<C>> <<D>> <<TypeRefl>> <<Palier>> <<MethodRefl>> ;

  Track := TRIVAT: GeoRes ::
     MAXR 1000000 DUAL 2 3 ;

  System := TRIVAA: MacroTot Track ;

  Flux := FLUD: System Track ::
    EDIT 1 ADI 4 EXTE 1.0E-6 ACCE 5 3 ;

  EVALUATE Prel := 1.0E-9 ; ! 0% PN
  EVALUATE Ptot := Pnom Prel * ;

  IF iGrappe 1 = THEN ! C and D banks inserted
    PowerCD Fmap := FLPOW: Fmap Flux Track Matex ::
      EDIT 2 PTOT <<Ptot>> PRINT DISTR POWER ;
  ELSEIF iGrappe 2 = THEN ! D bank inserted
    PowerD Fmap := FLPOW: Fmap Flux Track Matex ::
      EDIT 2 PTOT <<Ptot>> PRINT DISTR POWER ;
  ELSEIF iGrappe 3 = THEN ! ARO
    PowerARO Fmap := FLPOW: Fmap Flux Track Matex ::
      EDIT 2 PTOT <<Ptot>> PRINT DISTR POWER ;
  ENDIF ;

  Thermo Fmap := THM: Fmap ::
    EDIT 3
    ASSMB  204 21
    CWSECT <<coresect>> <<FlowRate>>
    INLET  <<OpePressure>> <<Tinlet>>
    RADIUS <<R_Pel>> <<R_Pin_ID>> <<R_Pin_OD>> <<R_Tub_OD>> ;

  MicroFuel MacroTot Track System Flux Thermo := DELETE:
  MicroFuel MacroTot Track System Flux Thermo ;
  EVALUATE iGrappe := iGrappe 1 + ;
ENDWHILE ;

END: ;
QUIT "LIST" .
