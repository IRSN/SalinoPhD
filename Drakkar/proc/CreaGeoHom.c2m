*DECK CreaGeoHom.c2m
*======================================================
* Name:   CreaGeoHom.c2m
* Type:   DRAGON procedure
* Use:    Create various geometry for homogeneisation
* Author: M. Cordiez 08.2015
*         [Altran | IRSN/PSN-EXP/SNC/LNR]
*------------------------------------------------------
* Procedure called as:
*------------------------------------------------------
* Input Required:
*
* Output Provided:
*  < Geometry objects for homogeneised assemblies
*======================================================
PARAMETER   GeoAssTriv    GeoAssHom
            GeoCPCPlie GeoCPCDeplie ::
        ::: LINKED_LIST
            GeoAssTriv    GeoAssHom
            GeoCPCPlie GeoCPCDeplie ; ;
MODULE      GEO:  ABORT: END: GREP: DELETE: ;
PROCEDURE   GetTechData ThermaExpans ;
*------------------------------------------------------
* Variables & Parameters
*------------------------------------------------------
STRING  Gestion AssType InfoGado ;
INTEGER imax := 1000000 ;
REAL    tmod ;
REAL    tmod_pAss ;
REAL    Rpellet
        Rfuel1  Rfuel2  Rfuel3  Rfuel4
        RGd1    RGd2    RGd3    RGd4    RGd5    RGd6
        rCladFuel   rTubeIn rTubeEx rRodAICn    rRodSSEx  rRodB4C rCladRod
        hWaterGap    pPin  pAss  pPinWaterGap   CoteB CoteBNeg ;

*------------------------------------------------------
* Recuperation des variables
*------------------------------------------------------
:: >>Gestion<< >>tmod<< ;
INTEGER DUMMY_I := 0 ;
REAL DUMMY_R0 := 0. ;
STRING DUMMY_S := "UOX" ;
*------------------------------------------------------
* Clad:  Mix description includes void between fuel/rod & clad
*     => rCladFuel = External Cladding Radius
*
* Lame:  Assembly "half" pitch [adjusted to hot conditions]
* Cote:  Central pin cell side length
* CoteL: Side pin cell side length [cote+lame]


LINKED_LIST Distances DeplParamAvg ;
Distances := GetTechData :: <<Gestion>> <<DUMMY_S>> <<DUMMY_I>>
  'None' 'DISTANCES' ;
DeplParamAvg := GetTechData :: <<Gestion>> <<DUMMY_S>> <<DUMMY_I>>
  'None' 'DEPLPARAMAVG' ;


!EVALUATE tmod_pAss := tmod
GREP: DeplParamAvg :: GETVAL 'tmod' 1 >>tmod_pAss<< ;


GREP: Distances :: GETVAL 'pAss' 1 >>pAss<< ;
GREP: Distances :: GETVAL 'pPin' 1 >>pPin<< ;
  ThermaExpans :: <<pAss>> "SS304" <<tmod_pAss>> "Distance" "LAME"
                  <<DUMMY_R0>> <<DUMMY_R0>>
                  >>pAss<< ;
  ThermaExpans :: <<pPin>> "Zr4" <<tmod>> "Distance" "PITCH"
                  <<DUMMY_R0>> <<DUMMY_R0>>
                  >>pPin<< ;
EVALUATE hWaterGap := pAss pPin 17. * - 2. / ;
EVALUATE pPinWaterGap   := pPin hWaterGap + ;

REAL     Cote1   := pPin      ;
REAL     Cote2   := pPin 2. * ;
REAL     Cote3   := pPin 3. * ;
REAL     Cote4   := pPin 4. * ;
REAL     Cote5   := pPin 5. * ;
REAL     Cote6   := pPin 6. * ;
REAL     Cote7   := pPin 7. * ;
REAL     Cote8   := pPin 8. * ;
REAL     Cote1Neg   := 0.0 ;
REAL     Cote2Neg   := Cote1 -1. * ;
REAL     Cote3Neg   := Cote2 -1. * ;
REAL     Cote4Neg   := Cote3 -1. * ;
REAL     Cote5Neg   := Cote4 -1. * ;
REAL     Cote6Neg   := Cote5 -1. * ;
REAL     Cote7Neg   := Cote6 -1. * ;
REAL     Cote8Neg   := Cote7 -1. * ;

*------------------------------------------------------
* DEFINITION DES GEOMETRIES
*------------------------------------------------------
IF Gestion "TIH" = THEN ! Geo15x15
*------------------------------------------------------
* GeoAssTriv : assemblage homogene pour suivi TRIVAT
*------------------------------------------------------
  EVALUATE CoteB      := Cote7 pPinWaterGap + ;
  EVALUATE CoteBNeg   := Cote7 -1. * hWaterGap - ;

  GeoAssTriv := GEO: :: CAR2D 8 8
  X- DIAG X+ REFL
  Y- SYME Y+ DIAG
  MIX  1  1  1  1  1  1  1  1
          1  1  1  1  1  1  1
             1  1  1  1  1  1
                1  1  1  1  1
                   1  1  1  1
                      1  1  1
                         1  1
                            1

  MESHX 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
           <<Cote6>> <<Cote7>> <<CoteB>>
  MESHY 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
           <<Cote6>> <<Cote7>> <<CoteB>>
;

*------------------------------------------------------
* GeoAssHom : assemblage homogene pour suivi TRIVAT
*------------------------------------------------------
  GeoAssHom := GEO: :: CAR2D 1 1
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX  1
    MESHX <<CoteBNeg>> <<CoteB>>
    MESHY <<CoteBNeg>> <<CoteB>>
  ;

*------------------------------------------------------
* GeoCPC  : assemblage crayon par crayon DEPLIE (important) pour EDI
*------------------------------------------------------
  GeoCPCDeplie := GEO: :: CAR2D 15 15
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX
   36  35  33  30  26  21  15   8  15  21  26  30  33  35  36
   35  34  32  29  25  20  14   7  14  20  25  29  32  34  35
   33  32  31  28  24  19  13   6  13  19  24  28  31  32  33
   30  29  28  27  23  18  12   5  12  18  23  27  28  29  30
   26  25  24  23  22  17  11   4  11  17  22  23  24  25  26
   21  20  19  18  17  16  10   3  10  16  17  18  19  20  21
   15  14  13  12  11  10   9   2   9  10  11  12  13  14  15
    8   7   6   5   4   3   2   1   2   3   4   5   6   7   8
   15  14  13  12  11  10   9   2   9  10  11  12  13  14  15
   21  20  19  18  17  16  10   3  10  16  17  18  19  20  21
   26  25  24  23  22  17  11   4  11  17  22  23  24  25  26
   30  29  28  27  23  18  12   5  12  18  23  27  28  29  30
   33  32  31  28  24  19  13   6  13  19  24  28  31  32  33
   35  34  32  29  25  20  14   7  14  20  25  29  32  34  35
   36  35  33  30  26  21  15   8  15  21  26  30  33  35  36
    MESHX    <<CoteBNeg>> <<Cote7Neg>> <<Cote6Neg>>
             <<Cote5Neg>> <<Cote4Neg>> <<Cote3Neg>> <<Cote2Neg>>
             <<Cote1Neg>>
             <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<CoteB>>
    MESHY    <<CoteBNeg>> <<Cote7Neg>> <<Cote6Neg>>
             <<Cote5Neg>> <<Cote4Neg>> <<Cote3Neg>> <<Cote2Neg>>
             <<Cote1Neg>>
             <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<CoteB>>
  ;
*------------------------------------------------------
* GeoCPC  : assemblage crayon par crayon pour suivi TRIVAT
*------------------------------------------------------
  GeoCPCPlie := GEO: :: CAR2D 8 8
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX   1  2  3  4  5  6  7  8
             9 10 11 12 13 14 15
               16 17 18 19 20 21
                  22 23 24 25 26
                     27 28 29 30
                        31 32 33
                           34 35
                              36
    MESHX 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<CoteB>>
    MESHY 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<CoteB>>
  ;
ELSE
! Geo17x17
*------------------------------------------------------
* GeoAssTriv : assemblage homogene pour suivi TRIVAT
*------------------------------------------------------
  EVALUATE CoteB      := Cote8 pPinWaterGap + ;
  EVALUATE CoteBNeg   := Cote8 -1. * hWaterGap - ;

  GeoAssTriv := GEO: :: CAR2D 9 9
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX  1  1  1  1  1  1  1  1  1
            1  1  1  1  1  1  1  1
               1  1  1  1  1  1  1
                  1  1  1  1  1  1
                     1  1  1  1  1
                        1  1  1  1
                           1  1  1
                              1  1
                                 1
    MESHX 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<Cote8>> <<CoteB>>
    MESHY 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<Cote8>> <<CoteB>>
  ;

*------------------------------------------------------
* GeoAssHom : assemblage homogene pour suivi TRIVAT
*------------------------------------------------------
  GeoAssHom := GEO: :: CAR2D 1 1
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX  1
    MESHX <<CoteBNeg>> <<CoteB>>
    MESHY <<CoteBNeg>> <<CoteB>>
  ;

*------------------------------------------------------
* GeoCPC  : assemblage crayon par crayon DEPLIE (important) pour EDI
*------------------------------------------------------
  GeoCPCDeplie := GEO: :: CAR2D 17 17
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX
   45  44  42  39  35  30  24  17   9  17  24  30  35  39  42  44  45
   44  43  41  38  34  29  23  16   8  16  23  29  34  38  41  43  44
   42  41  40  37  33  28  22  15   7  15  22  28  33  37  40  41  42
   39  38  37  36  32  27  21  14   6  14  21  27  32  36  37  38  39
   35  34  33  32  31  26  20  13   5  13  20  26  31  32  33  34  35
   30  29  28  27  26  25  19  12   4  12  19  25  26  27  28  29  30
   24  23  22  21  20  19  18  11   3  11  18  19  20  21  22  23  24
   17  16  15  14  13  12  11  10   2  10  11  12  13  14  15  16  17
    9   8   7   6   5   4   3   2   1   2   3   4   5   6   7   8   9
   17  16  15  14  13  12  11  10   2  10  11  12  13  14  15  16  17
   24  23  22  21  20  19  18  11   3  11  18  19  20  21  22  23  24
   30  29  28  27  26  25  19  12   4  12  19  25  26  27  28  29  30
   35  34  33  32  31  26  20  13   5  13  20  26  31  32  33  34  35
   39  38  37  36  32  27  21  14   6  14  21  27  32  36  37  38  39
   42  41  40  37  33  28  22  15   7  15  22  28  33  37  40  41  42
   44  43  41  38  34  29  23  16   8  16  23  29  34  38  41  43  44
   45  44  42  39  35  30  24  17   9  17  24  30  35  39  42  44  45
    MESHX    <<CoteBNeg>> <<Cote8Neg>> <<Cote7Neg>> <<Cote6Neg>>
             <<Cote5Neg>> <<Cote4Neg>> <<Cote3Neg>> <<Cote2Neg>>
             <<Cote1Neg>>
             <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<Cote8>> <<CoteB>>
    MESHY    <<CoteBNeg>> <<Cote8Neg>> <<Cote7Neg>> <<Cote6Neg>>
             <<Cote5Neg>> <<Cote4Neg>> <<Cote3Neg>> <<Cote2Neg>>
             <<Cote1Neg>>
             <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<Cote8>> <<CoteB>>
  ;
*------------------------------------------------------
* GeoCPC  : assemblage crayon par crayon pour suivi TRIVAT
*------------------------------------------------------
  GeoCPCPlie := GEO: :: CAR2D 9 9
    X- DIAG X+ REFL
    Y- SYME Y+ DIAG
    MIX   1  2  3  4  5  6  7  8  9
            10 11 12 13 14 15 16 17
               18 19 20 21 22 23 24
                  25 26 27 28 29 30
                     31 32 33 34 35
                        36 37 38 39
                           40 41 42
                              43 44
                                 45
    MESHX 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<Cote8>> <<CoteB>>
    MESHY 0. <<Cote1>> <<Cote2>> <<Cote3>> <<Cote4>> <<Cote5>>
             <<Cote6>> <<Cote7>> <<Cote8>> <<CoteB>>
  ;
ENDIF ;
*------------------------------------------------------
END: ;
QUIT "LIST" .
